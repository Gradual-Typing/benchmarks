;; Simple streams library.
;; For building and using infinite lists.

;; A stream is a cons of a value and a thunk that computes the next
;; value when applied
;; (define-type stream (Rec s (Tuple Int (-> s))))

(define (stream-first [st : (Rec s (Tuple Int (-> s)))])
  : Int
  (tuple-proj st 0))

(define (stream-rest [st : (Rec s (Tuple Int (-> s)))])
  : (Rec s (Tuple Int (-> s)))
  ((tuple-proj st 1)))

;;------------------------------------------------------------------------------

(define (make-stream [hd : Int] [thunk : (-> (Rec s (Tuple Int (-> s))))])
  : (Rec s (Tuple Int (-> s)))
  (tuple hd thunk))

;; Destruct a stream into its first value and the new stream produced
;; by de-thunking the tail
(define (stream-unfold [st : (Rec s (Tuple Int (-> s)))])
  : (Tuple Int (Rec s (Tuple Int (-> s))))
  (tuple (stream-first st) (stream-rest st)))

;; [stream-get st i] Get the [i]-th element from the stream [st]
(define (stream-get [st : (Rec s (Tuple Int (-> s)))] [i : Int]) : Int
  ;; changed recursive loop to iterative one
  ;; because we are not performing tail call optimization.
  ;; 5x performance increase.
  (stream-first
   (repeat (i 0 i) (st : (Rec s (Tuple Int (-> s))) st) (stream-rest st))))

;; Use the partner file "streams.rkt" to implement the Sieve of
;; Eratosthenes.  Then compute and print the 10,000th prime number.

;;--------------------------------------------------------------------------------------------------

;; `count-from n` Build a stream of integers starting from `n` and
;; iteratively adding 1

(define (count-from [n : Int]) : (Rec s (Tuple Int (-> s)))
  (make-stream n (lambda () (count-from (+ 1 n)))))

;; `sift n st` Filter all elements in `st` that are equal to `n`.
;; Return a new stream.
(define (sift [n : Int] [st : (Rec s (Tuple Int (-> s)))])
  : (Rec s (Tuple Int (-> s)))
  (let ([hd : Int (stream-first st)])
    (cond
      [(= 0 (%% hd n))
       (sift n (stream-rest st))]
      [else
       (make-stream hd (lambda () (sift n (stream-rest st))))])))

;; `sieve st` Sieve of Eratosthenes
(define (sieve [st : (Rec s (Tuple Int (-> s)))])
  : (Rec s (Tuple Int (-> s)))
  (let ([hd : Int (stream-first st)])
    (make-stream hd (lambda () (sieve (sift hd (stream-rest st)))))))

;; stream of prime numbers
(define primes : (Rec s (Tuple Int (-> s))) (sieve (count-from 2)))

(define (main) : Unit
  (let ([N-1 : Int (read-int)])
    (print-int (stream-get primes N-1))
    (display-char #\newline)))

(time (main))
